plugins {
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'idea'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'maven-publish'
}

def envVersion = System.getenv('VERSION')
version = envVersion == null ? '0.0.0-indev' : envVersion
group = 'com.eerussianguy.betterfoliage' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'betterfoliage'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'official', version: '1.20.1'
    runs {
        client {
            workingDirectory project.file('run')

            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'

            mods {
                betterfoliage {
                    source sourceSets.main
                }
            }
        }

    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.1.0'

    //runtimeOnly fg.deobf('bop:BiomesOPlenty-1.18.2:16.0.0.109-universal')
    //runtimeOnly fg.deobf('tb:TerraBlender-forge-1.18.2:1.1.0.102')
}

repositories {
    flatDir {
        dir 'libs'
    }
}

// Example for how to get properties into the manifest for reading by the runtime..
jar {
    manifest {
        attributes([
            "Specification-Title": "betterfoliage",
            "Specification-Vendor": "eerussianguy",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor" :"eerussianguy",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

curseforge {
    final String envApiKey = System.getenv('CURSEFORGE_API_KEY');
    final String versionType = 'release'

    apiKey = envApiKey == null ? 'nope' : envApiKey

    project {
        id = '470013'
        changelog = "[Changelog ${version}](https://github.com/eerussianguy/Better-Foliage/releases/tag/v${project.version})"
        changelogType = "markdown"

        releaseType = versionType
        addGameVersion "1.20.1"

        mainArtifact(jar) {
            displayName = System.getenv('VERSION').toString().replace('v', '')
        }

        relations {
            optionalDependency 'terrafirmacraft'
        }
    }
}
